buildscript {
    dependencies {
        classpath 'org.fusesource.jansi:jansi:1.18'
    }
}

plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'idea'
    id 'jacoco'
    id 'com.gradle.plugin-publish' version '0.12.0'
    id 'com.github.kt3k.coveralls' version '2.9.0'
}

def thisPlugin =
    new GroovyScriptEngine(file('src/main/groovy').absolutePath, this.class.classLoader)
        .loadScriptByName('com/adarshr/gradle/testlogger/TestLoggerPlugin.groovy')

apply from: 'gradle/publishing.gradle'
apply from: 'gradle/coverage.gradle'
apply plugin: thisPlugin

repositories {
    mavenCentral()
}

sourceSets {
    functionalTest {
        groovy {
            srcDir 'src/test-functional/groovy'
            compileClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.output
        }
    }
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation 'org.fusesource.jansi:jansi:1.18'

    testImplementation gradleTestKit()
    testImplementation('org.spockframework:spock-core:1.3-groovy-2.5') {
        exclude module: 'groovy-all'
    }
    testImplementation 'net.bytebuddy:byte-buddy:1.10.1'
    testImplementation 'org.objenesis:objenesis:3.1'
    testImplementation 'commons-io:commons-io:2.8.0'

    functionalTestImplementation localGroovy()
    functionalTestImplementation gradleTestKit()
    functionalTestImplementation('org.spockframework:spock-core:1.3-groovy-2.5') {
        exclude module: 'groovy-all'
    }
    functionalTestImplementation 'commons-io:commons-io:2.6'
}

gradlePlugin {
    plugins {
        testLoggerPlugin {
            id = 'com.adarshr.test-logger'
            implementationClass = 'com.adarshr.gradle.testlogger.TestLoggerPlugin'
        }
    }
}

idea {
    module {
        testSourceDirs += file('src/test-functional/groovy')
    }
}

test {
    testClassesDirs += sourceSets.functionalTest.output.classesDirs
    classpath += sourceSets.functionalTest.runtimeClasspath
    systemProperty 'file.encoding', 'UTF-8'

    testlogger {
        theme 'mocha'
    }

    exclude 'com/adarshr/gradle/testlogger/functional/**'
}

task functionalTest(type: Test) {
    inputs.dir "$projectDir/src/test-functional/resources"
    maxParallelForks = 2
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath + sourceSets.functionalTest.runtimeClasspath
    systemProperty 'file.encoding', 'UTF-8'
    minHeapSize '128m'
    maxHeapSize '512m'

    testlogger {
        theme 'mocha-parallel'
    }

    include 'com/adarshr/gradle/testlogger/functional/**'
}

tasks.check.dependsOn 'functionalTest'
tasks.functionalTest.dependsOn 'pluginUnderTestMetadata', 'testClasses'
tasks.functionalTest.mustRunAfter 'test'
